/*ª*REXX program calculates the  Nth root  of  X,  with  DIGS  (decimal digits) accuracy. */
parse arg x root digs .                          /*ª*obtain optional arguments from the CL*/
if    x=='' |    x==","   then    x= 2           /*ª*Not specified?  Then use the default.*/
if root=='' | root==","   then root= 2           /*ª* "       "        "   "   "      "   */
if digs=='' | digs==","   then digs=65           /*ª* "       "        "   "   "      "   */
numeric digits digs                              /*ª*set the  decimal digits  to   DIGS.  */
say '       x = '    x                           /*ª*echo the value of   X.               */
say '    root = '    root                        /*ª*  "   "    "    "   ROOT.            */
say '  digits = '    digs                        /*ª*  "   "    "    "   DIGS.            */
say '  answer = '    root(x, root)               /*ª*show the value of   ANSWER.          */
exit                                             /*ª*stick a fork in it,  we're all done. */
/*ª*â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€*/
root: procedure;  parse arg x 1 Ox, r 1 Or             /*ª*arg1 â”€â”€â–º x & Ox, 2nd â”€â”€â–º r & Or*/
if r==''  then r=2                               /*ª*Was root specified?  Assume âˆš. */
if r=0    then return '[n/a]'                    /*ª*oops-ay!  Can't do zeroth root.*/
complex= x<0 & R//2==0                           /*ª*will the result be complex?    */
oDigs=digits()                                   /*ª*get the current number of digs.*/
if x=0 | r=1  then return x/1                    /*ª*handle couple of special cases.*/
dm=oDigs+5                                       /*ª*we need a little guard room.   */
r=abs(r);   x=abs(x)                             /*ª*the absolute values of R and X.*/
rm=r-1                                           /*ª*just a fast version of  ROOT -1*/
numeric form                                     /*ª*take a good guess at the rootâ”€â”*/
parse value format(x,2,1,,0) 'E0' with newV1 'E' newV2 . /*ª* â—„â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜*/
g= (newV1 / r'E'newV2 % r)  +  (x>1)                     /*ª*kinda uses a crude "logarithm".*/
d=5                                              /*ª*start with five decimal digits.*/
do until d==dm;   d=min(d+d,dm)             /*ª*each time,  precision doubles. */
numeric digits d                            /*ª*tell REXX to use   D   digits. */
old=-1                                      /*ª*assume some kind of old guess. */
do until old=g;   old=g              /*ª*where da rubber meets da roadâ”€â”*/
g=format((rm*g**r+x)/r/g**rm,, d-2)  /*ª* â—„â”€â”€â”€â”€â”€â”€ the root computationâ”€â”˜*/
end   /*ª*until old=g*/                /*ª*maybe until the cows come home.*/
end          /*ª*until d==dm*/                /*ª*and wait for more cows to come.*/

if g=0        then return 0                      /*ª*in case the jillionth root = 0.*/
if Or<0       then g=1/g                         /*ª*root < 0 ?   Reciprocal it is! */
if \complex   then g=g*sign(Ox)                  /*ª*adjust the sign  (maybe).      */
numeric digits oDigs                             /*ª*reinstate the original digits. */
return (g/1)  ||  left('j', complex)             /*ª*normalize # to digs, append j ?*/
/* -------------------------------------------------------------------------
 * REXX C:\Users\PeterJ\rexxetta\rexx\Nth-root\nth-root.rexx was migrated on 23 Mar 2025 at 10:43:22
 * -------------------------------------------------------------------------
*/ 
